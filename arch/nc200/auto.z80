    maclib nc200

    org 0x4000

; On entry, 0x0000 contains bank 0x40 and 0x4000 contains (usually) 0x47. This
; means we can write to 0x0000 without stepping on ourselves.

main:
    ; Interrupts *off*! We want to kill the NC200 OS ASAP.

    di

    ; Now, load the entire first track to 0x0000. The 765 has just loaded
    ; this boot block so we know it's correctly seeked and spun up. We
    ; just need to issue the command and we're ready.

    ; Send the read command to the 765.

    ld hl, data.readtrack
    ld b, data.readtrack.end - data.readtrack
    ld c, PORT_NEC765_DATA
cmd_loop:
	in a, (PORT_NEC765_STATUS)
	rla							; RQM...
	jr nc, cmd_loop             ; ...low, keep waiting
    outi                        ; port(C) = (HL++), B++
    jr nz, cmd_loop

    ; Now read bytes from the 765 until we're done.

	ld hl, 0                   ; destination address
	; ld c, PORT_NEC765_DATA   ; still in C from previously
read_loop:
	in a, (PORT_NEC765_STATUS)
	rla							; RQM...
	jr nc, read_loop      		; ...low, keep waiting
	rla							; DIO (ignore)
	rla							; EXM...
	jr nc, read_finished		; ...low, transfer complete
	ini							; (HL++) = port(C), B--
    jr read_loop
read_finished:

    ; We've finished a command, so nudge the FDC TC line to tell it
    ; we're done.

	ld a, 0x83
	out (PORT_WAIT_STATE_CONTROL), a
	dec a
	out (PORT_WAIT_STATE_CONTROL), a

    ; Read and then discard the status.

read_status_loop:
	in a, (PORT_NEC765_STATUS)
	rla 						; RQM...
	jr nc, read_status_loop 	; ...low, keep waiting 
	rla							; DIO...
    jr nc, finished             ; ...low, no more data
    in a, (PORT_NEC765_DATA)
	jr read_status_loop

    ; We're done.

finished:
    ; Jump to the newly loaded code.
    jp 0

; The baked command to read a track.

data.readtrack:
    db 0xc2     ; 0: READ TRACK MULTITRACK
    db 0        ; 1: physical head 0, drive 0
    db 0        ; 2: track 0
    db 0        ; 3: logical head 0
    db 0        ; 4: unused
    db 2        ; 5: bytes per sector: 512
    db 9        ; 6: last sector for one track (*inclusive*)
    db 27       ; 7: gap 3 length (27 is standard for 3.5" drives)
    db 0        ; 8: sector length (unused)
data.readtrack.end:
