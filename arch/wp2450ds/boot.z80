; Brother WP2450DS cpmish BIOS Â© 2019 David Given
; This file is distributable under the terms of the 2-clause BSD license.
; See COPYING.cpmish in the distribution root directory for more information.

; This is the executable which is loaded and run by the Brother's OS. It
; gets loaded with BBR=CBR=0x60, CBAR=0x55, which means that DRAM from
; physical addresses 0x65000 to 0x6ffff is mapped to logical 0x5000 to 0xffff.

    maclib wp2450ds
    maclib cpmish

    extrn FDCAL2
    extrn FDTX
    extrn FDR512
    extrn FDRSTAT
    extrn FDDEBLK

start:
    dw 0xc181
    dw 0x0101
    dw 0x0000
    db "BR"

    di                  ; The Brother OS is not at home

    xor a
    out0a CBAR          ; now the entire address space is RAM
    ld sp, 0x100        ; startup stack

    call FDCAL2
    ld a, 0x03          ; SPECIFY
    call FDTX
    ld a, 0xdf          ; SRT, HUT
    call FDTX
    ld a, 0x0d          ; HLT, ND
    call FDTX
    call FDRSTAT

    ld d, 1             ; current track
    ld e, 0             ; current sector
    ld hl, LOAD_ADDRESS ; destination address
.1
    push de
    push hl
    call FDR512
    pop de
    ld bc, 512
    ldir                ; copy buffer data to memory
    ex de, hl           ; updated destination address to HL
    pop de

    inc e
    ld a, 18
    cp e
    jr nz, .1           ; loop again

    jp BIOS_ADDRESS

; vim: ts=4 sw=4 et ft=asm

