; Brother 'OP2' cpmish BIOS Â© 2019 David Given
; This file is distributable under the terms of the 2-clause BSD license.
; See COPYING.cpmish in the distribution root directory for more information.

; This is the executable which is loaded and run by the Brother's OS. It
; gets loaded with BBR=CBR=0x60, CBAR=0x55, which means that DRAM from
; physical addresses 0x65000 to 0x6ffff is mapped to logical 0x5000 to 0xffff.

	maclib brotherop2
	maclib cpmish

start:
	dw MAGIC_OP2

	; In order to safely make system calls we need to show the system ROM
	; at 0x4000-0x7fff. We're currently mapped in at 0x5000, so let's
	; relocate to 0x8000 and adjust the mapping. 
	;
	; IMPORTANT! Remember that
	; even though we're loaded at 0x5000, we've been assembled at 0x8000
	; and addresses will all be wrong.

	; Physically copy our code.

	ld hl, 0x5000
	ld de, 0x8000
	ld bc, end - start
	ldir

	; Switch to running code from the new location.

	jp switch_to_8000
switch_to_8000:

	; ...and raise the B and C boundaries to 0x8000 too.

	ld a, 0x88
	out0a CBAR

	call tty_init

.1
	ld a, 5 ; get a key
	rst 8
	jr nc, .1
	ld a, d
	cp 0x8f
	jr z, .1
	call tty_puthex8
	jr .1

.2
	rst 0
	jr $

EMULATE_CLEAR_TO_EOL = 1
EMULATE_CLEAR_TO_EOS = 1
	maclib tty

tty_scroll:
tty_insert_line:
tty_delete_line:
	ret

tty_rawwrite:
	ld hl, (tty_cursorx)
	out0h PORT_VIDEO_Y
	ld h, 0
	add hl, hl
	out0l PORT_VIDEO_X
	cp a, ' '
	jr nz, .1
	xor a
.1
	ld l, 0
	out0l PORT_VIDEO_DATA
	out0a PORT_VIDEO_DATA
	ret

str.banner:
	cpmish_banner 'Brother OP2'

end:

